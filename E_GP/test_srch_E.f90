! MAIN PROGRAM FOR NUMERICAL EXPERIMENT E IN THE PAPER "Spectrum-Revealing Cholesky Factorization for Kernel Methods"

! COMPARE SRCH, DPSTRF ON CCPP DATA

! N: NUMBER OF SAMPLES IN TRAINING DATA
! NTEST: NUMBER OF SAMPLES IN TESTING DATA
! D: NUMBER OF FEATURES
! NP: OVERSAMPLING
! BLOCKSZIE: BLOCK SIZE
! R: TARGET RANK

PROGRAM TEST_SRCH_E

	CHARACTER(10) ::    TOL_STRING, R_STRING
	INTEGER, PARAMETER :: N = 5000
	INTEGER, PARAMETER :: NTEST = 4568
	INTEGER, PARAMETER :: D = 4
	INTEGER, PARAMETER :: NP = 25
	INTEGER, PARAMETER :: BLOCKSIZE = 20
	INTEGER :: R
	INTEGER :: R_DPOTRF

	REAL*8, PARAMETER :: ONE = 1.0_8, ZERO = 0.0_8

	REAL*8 :: TOL
	REAL*8 :: LAMBDA = 0.00005
	REAL*8 :: SIGMA = 2.0

	INTEGER :: ISEED(4)
	INTEGER :: P(N), P_DPSTRF(N)
	INTEGER :: R_DPSTRF 
	REAL*8, DIMENSION(N,N) :: A, L, A_DPSTRF, L_DPSTRF

	REAL*8, DIMENSION(:,:), ALLOCATABLE :: ATEST, Y, YTEST, B, B1, C1, B2, C2, W, ORIGINAL
	REAL*8, DIMENSION(:,:), ALLOCATABLE :: YTEST_PREDICTION, A1_STAR, L11, LTEMP, YTEMP, MIDDLE, TEMP
	REAL*8 :: WORK(5*N), DLANGE, START, FINISH

	INTEGER :: I, J, INFO

	ALLOCATE(ATEST(NTEST,N), Y(N,1), YTEST(NTEST,1), W(NP,N), ORIGINAL(N,N))
	ALLOCATE(B(N,D), B1(N,D), C1(N,N), B2(NTEST,D), C2(NTEST,N))

	ISEED(1) = 23
	ISEED(2) = 67
	ISEED(3) = 54
	ISEED(4) = 24
	! CREATE RANDOM MATRIX W
	CALL DLARNV(3, ISEED, NP * N, W)

	CALL GETARG(1, TOL_STRING)
	READ(TOL_STRING, *) TOL
	CALL GETARG(2, R_STRING)
	READ(R_STRING, *) R
	WRITE (*,*) "TOL IS", TOL 


	OPEN(UNIT = 1, FILE = "CCPP_train_data_5000_4.txt", ACTION = "READ")
	DO I = 1,N
		READ(1,*) (B(I,J), J = 1,D)
	END DO
	B1 = B

	CALL GENERATE_DISTANCE_MATRIX(B1, N, D, C1)
	CALL GENERATE_RBF_KERNEL(C1, N, SIGMA, A)
	A = 0.5 * (A + TRANSPOSE(A))
	! ADD REGULARIZATION
	DO I = 1,N
		A(I,I) = A(I,I) + LAMBDA
	END DO
	ORIGINAL = A

	OPEN(UNIT = 2, FILE = "CCPP_train_label_5000_1.txt", ACTION = "READ")
	DO I = 1,N
		READ(2,*) Y(I,1)
	END DO

	OPEN(UNIT = 3, FILE = "CCPP_test_data_4568_4.txt", ACTION = "READ")
	DO I = 1,NTEST
		READ(3,*) (B2(I,J), J = 1,D)
	END DO

	B1 = B
	CALL GENERATE_DISTANCE_MATRIX1_MATRIX2(B2, B1, NTEST, N, D, C2)
	CALL GENERATE_RBF_KERNEL_MATRIX1_MATRIX2(C2, NTEST, N, SIGMA, ATEST)

	OPEN(UNIT = 4, FILE = "CCPP_test_label_4568_1.txt", ACTION = "READ")
	DO I = 1,NTEST
		READ(4,*) YTEST(I,1)
	END DO

	! CALL SRCH
	! NO NEED TO DO EXTRA SWAPS
	CALL CPU_TIME(START)
	CALL RANDOM_PROJECTION_CH(A, N, W, BLOCKSIZE, NP, R, P)
	CALL CPU_TIME(FINISH)
	WRITE (*,*) 'RANK OF SRCH IS', R
	WRITE (*,*) 'RUNNING TIME OF SRCH IS', FINISH - START

	! CALL DPSTRF
	A_DPSTRF = ORIGINAL
	CALL CPU_TIME(START)
	CALL DPSTRF( 'L', N, A_DPSTRF, N, P_DPSTRF, R_DPSTRF, TOL, WORK, INFO )
	CALL CPU_TIME(FINISH)
	WRITE (*,*) 'RANK OF DPSTRF IS', R_DPSTRF 
	WRITE (*,*) 'RUNNING TIME OF DPSTRF IS', FINISH - START

	! COPY L FACTOR OF SRCH TO L
	L = 0.0_8
	DO I = 1,N
		DO J = 1,MIN(I,R)
			L(I,J) = A(I,J)
		END DO
	END DO

	! COPY L FACTOR OF DPSTRF TO L_DPSTRF
	L_DPSTRF = 0.0_8
	DO I = 1,N
		DO J = 1,MIN(I,R_DPSTRF)
			L_DPSTRF(I,J) = A_DPSTRF(I,J)
		END DO
	END DO

	! DO PREDICTION USING SRCH

	! COMPUTE YTEST_PREDICTION
	! YTEST_PREDICTION = A1_STAR * L11^{-T} * (LAMBDA^2I + LTEMP' * LTEMP)^{-1} * LTEMP' * YTEMP
	ALLOCATE(YTEST_PREDICTION(NTEST,1), A1_STAR(NTEST,R), L11(R,R), LTEMP(N,R), YTEMP(N,1), MIDDLE(R,R), TEMP(R,1))
	A1_STAR = ATEST(1:NTEST,P(1:R))
	L11 = L(1:R,1:R)
	LTEMP = L(1:N,1:R)
	YTEMP = Y(P,:)

	! ASSIGAN A1_STAR = A1_STAR * L11^{-T}
	CALL DTRSM('R','L','T','N',NTEST,R,ONE,L11,R,A1_STAR,NTEST)

	! MIDDLE = LAMBDA^2I + LTEMP' * LTEMP
	MIDDLE = MATMUL(TRANSPOSE(LTEMP), LTEMP)
	DO I = 1,R
		MIDDLE(I,I) = MIDDLE(I,I) + LAMBDA
	END DO

	! WE NEED COMPUTE A_STAR * (MIDDLE)^{-1} * LTEMP' * YTEMP
	! TEMP = LTEMP' * YTEMP
	TEMP = MATMUL(TRANSPOSE(LTEMP), YTEMP)

	! TEMP = (MIDDLE)^{-1} * LTEMP' * YTEMP
	CALL DPOTRF('L', R, MIDDLE, R, INFO )
	CALL DPOTRS('L', R, 1, MIDDLE, R, TEMP, R, INFO )
	CALL DGEMM('N', 'N', NTEST, 1, R, ONE, A1_STAR, NTEST, TEMP, R, ZERO, YTEST_PREDICTION, NTEST)

	YTEST_PREDICTION = YTEST_PREDICTION - YTEST

	! WRITE (*,*) 'MEAN SQUARED PREDICTION ERROR FOR SRCH'
	WRITE (*,*) ((DLANGE('F', NTEST, 1, YTEST_PREDICTION, NTEST, WORK))**2) / NTEST

	DEALLOCATE(YTEST_PREDICTION, A1_STAR, L11, LTEMP, YTEMP, MIDDLE, TEMP)

	! DO PREDICTION USING DPSTRF

	! COMPUTE YTEST_PREDICTION
	! YTEST_PREDICTION = A1_STAR * L11^{-T} * (LAMBDA^2I + LTEMP' * LTEMP)^{-1} * LTEMP' * YTEMP
	ALLOCATE(YTEST_PREDICTION(NTEST,1), A1_STAR(NTEST,R_DPSTRF), L11(R_DPSTRF,R_DPSTRF), LTEMP(N,R_DPSTRF), &
		YTEMP(N,1), MIDDLE(R_DPSTRF,R_DPSTRF), TEMP(R_DPSTRF,1))
	A1_STAR = ATEST(1:NTEST,P_DPSTRF(1:R_DPSTRF))
	L11 = L_DPSTRF(1:R_DPSTRF,1:R_DPSTRF)
	LTEMP = L_DPSTRF(1:N,1:R_DPSTRF)
	YTEMP = Y(P_DPSTRF,:)

	! ASSIGAN A1_STAR = A1_STAR * L11^{-T}
	CALL DTRSM('R','L','T','N',NTEST,R_DPSTRF,ONE,L11,R_DPSTRF,A1_STAR,NTEST)

	! MIDDLE = LAMBDA^2I + LTEMP' * LTEMP
	MIDDLE = MATMUL(TRANSPOSE(LTEMP), LTEMP)
	DO I = 1,R_DPSTRF
		MIDDLE(I,I) = MIDDLE(I,I) + LAMBDA
	END DO

	! WE NEED COMPUTE A_STAR * (MIDDLE)^{-1} * LTEMP' * YTEMP
	! TEMP = LTEMP' * YTEMP
	TEMP = MATMUL(TRANSPOSE(LTEMP), YTEMP)

	! TEMP = (MIDDLE)^{-1} * LTEMP' * YTEMP
	CALL DPOTRF('L', R_DPSTRF, MIDDLE, R_DPSTRF, INFO )
	CALL DPOTRS('L', R_DPSTRF, 1, MIDDLE, R_DPSTRF, TEMP, R_DPSTRF, INFO )
	CALL DGEMM('N', 'N', NTEST, 1, R_DPSTRF, ONE, A1_STAR, NTEST, TEMP, R_DPSTRF, ZERO, YTEST_PREDICTION, NTEST)

	YTEST_PREDICTION = YTEST_PREDICTION - YTEST

	! WRITE (*,*) 'MEAN SQUARED PREDICTION ERROR FOR DPSTRF'
	WRITE (*,*) ((DLANGE('F', NTEST, 1, YTEST_PREDICTION, NTEST, WORK))**2) / NTEST
	WRITE (*,*)
	DEALLOCATE(YTEST_PREDICTION, A1_STAR, L11, LTEMP, YTEMP, MIDDLE, TEMP)

	DEALLOCATE(B, B1, B2, C1, C2)
	DEALLOCATE(ATEST, Y, YTEST, W, ORIGINAL)
END PROGRAM TEST_SRCH_E