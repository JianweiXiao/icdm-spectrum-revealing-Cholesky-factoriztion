! USE RANDOM PROJECTION PIVOTING STRATEGY TO GET A TRUNCATED R CHOLEKSY FACTORIZATION OF A
!
!   INPUTS:     A  - POSITIVE SEMIDEFINITE MATRIX
!               N  - SIZE OF MATRIX A 
!               W  - RANDOM PROJECTION MATRIX, DIMENSION IS NP BY N
!               BLOCKSIZE - BLOCK SIZE
!                    DOES NOT NEED TO DIVIDE M OR N
!               NP - OVERSAMPLING PARAMETER
!                    NP >= NB
!               R  - EXPECTED RANK
!
!   OUTPUTS:    L FACTOR = (TRIL(A))(1:N,1:N) (R+1:N COLUMNS WILL BE ZERO)
!               IPIV - PIVOTS
!

SUBROUTINE RANDOM_PROJECTION_CH(A, N, W, BLOCKSIZE, NP, R, IPIV)

	IMPLICIT NONE 

	INTEGER, INTENT(IN) :: N, NP, BLOCKSIZE, R
	REAL*8, INTENT(INOUT), DIMENSION(N,N) :: A 
	REAL*8, INTENT(INOUT), DIMENSION(NP,N) :: W
	INTEGER, INTENT(OUT), DIMENSION(N) :: IPIV

	INTEGER :: NB, ICOL, I, J, INFO, ITEMP, IN
	INTEGER, DIMENSION(N) :: IPIV_B
	REAL*8, DIMENSION(BLOCKSIZE) :: TAU_B
	REAL*8, DIMENSION(NP,N) :: B, BUPDATE, BTEMP
	REAL*8, ALLOCATABLE, DIMENSION(:) :: WORK
	REAL*8 :: ONE, ZERO
	PARAMETER( ONE = 1.0D+0, ZERO = 0.0D+0 )

	DO I = 1,N
		IPIV(I) = I
	END DO

	NB = BLOCKSIZE
	ALLOCATE(WORK(10*N))

	IF (NP < NB) THEN
		WRITE(*,*) 'INSUFFICIENT RANDOM SAMPLING'
	END IF

	! COMPUTE RANDOM PROJECTION B = W * A
	CALL DGEMM('N','N',NP,N,N,ONE,W,NP,A,N,ZERO,B,NP)

	! LOOP THROUGH LEADING COLUMNS
	DO ICOL = 1, R, NB
		NB = MIN(NB, R-ICOL+1)
		! INITIALIZE IPIV_B 
		IPIV_B = 0

		! COPY B(1:NP, ICOL:N) TO BTEMP(1:NP, ICOL:N)
		CALL DLACPY( 'ALL', NP, N-ICOL+1, B(1,ICOL), NP, BTEMP(1,ICOL), NP )

		! DO PARTIAL QRCP ON B TO FIND NB PIVOTS
		CALL PARTIAL_DGEQPF(NB, NP, N-ICOL+1, BTEMP(1, ICOL), NP, IPIV_B(ICOL), TAU_B, WORK)

		! APPLY THESE PIVOTS ON A
		DO I = ICOL, N
			IPIV_B( I ) = -IPIV_B( I )
		END DO

		DO I = ICOL, N
			IF( IPIV_B( I ).GT.0 ) THEN
				GO TO 30
			ELSE
			END IF

			J = I
			IPIV_B( J ) = -IPIV_B( J )
			IN = IPIV_B( J ) + ICOL - 1

			40 CONTINUE
			IF( IPIV_B( IN ).GT.0 ) THEN
				GO TO 30
			ELSE
			END IF

			IF (J .NE. IN) THEN
				CALL DSWAP( N-ICOL+1, A(ICOL, J), 1, A(ICOL, IN), 1 )
				CALL DSWAP( N, A(J, 1), N, A(IN, 1), N)
				CALL DSWAP( NP, W(1, J), 1, W(1, IN), 1)
				CALL DSWAP( NP, B(1, J), 1, B(1, IN), 1)

				ITEMP = IPIV(J)
				IPIV(J) = IPIV(IN)
				IPIV(IN) = ITEMP
			ELSE
			END IF

			IPIV_B( IN ) = -IPIV_B( IN )
			J = IN
			IN = IPIV_B( IN ) + ICOL - 1
			GO TO 40
			30 CONTINUE
		END DO

		! A(ICOL:N,ICOL:ICOL+NB-1) = A(ICOL:N, ICOL:ICOL+NB-1) - MATMUL(A(ICOL:N, 1:ICOL-1), TRANSPOSE(A(ICOL:ICOL+NB-1, 1:ICOL-1)))
		CALL DGEMM('N','T',N-ICOL+1,NB,ICOL-1,-ONE,A(ICOL,1),N,A(ICOL,1),N,ONE,A(ICOL,ICOL),N) 
		CALL DPOTF2('L', NB, A(ICOL,ICOL), N, INFO)

		! SET STRICTLY UPPER PART OF A(ICOL+NB:N,ICOL:ICOL-1+NB) TO BE ZERO
		CALL DLASET( 'U', NB-1, NB-1, 0.0_8, 0.0_8, A(ICOL,ICOL+1), N )
		! ORIGINAL CODE: A(ICOL+NB:N,ICOL:ICOL-1+NB) = A(ICOL+NB:N,ICOL:ICOL-1+NB) / TRANSPOSE(LBLOCK)
		CALL DTRSM('R', 'L', 'T', 'N', N-ICOL-NB+1, NB, ONE, A(ICOL,ICOL), N, A(ICOL+NB,ICOL), N)

		IF (ICOL+NB-1 < R ) THEN
			!   B(1:NP,ICOL+NB:N) = B(1:NP,ICOL+NB:N) - MATMUL(W(1:NP,ICOL:N), A(ICOL:N,ICOL:ICOL+NB-1)) * TRANSPOSE(A(ICOL+NB:N, ICOL:ICOL+NB-1))
			CALL DGEMM('N', 'N', NP, NB, N-ICOL+1, 1.0_8, W(1,NP), NP, A(ICOL,ICOL), N, 0.0_8, BUPDATE, NP)
			CALL DGEMM('N', 'T', NP, N-ICOL-NB+1, NB, -1.0_8, BUPDATE, NP, A(ICOL+NB, ICOL), N, 1.0_8, B, NP)
		END IF
	END DO
	DEALLOCATE(WORK)
END SUBROUTINE RANDOM_PROJECTION_CH
